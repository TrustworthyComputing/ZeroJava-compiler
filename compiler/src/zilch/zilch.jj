options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC = false;
}

PARSER_BEGIN(ZilchParser)
  public class ZilchParser {}
PARSER_END(ZilchParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < DOT: "." >
| < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < ADD: "+" >
| < ADDADD: "++" >
| < SUB: "-" >
| < SUBSUB: "--" >
| < AND : "&&" >
| < OR : "||" >
| < NOT : "!" >
| < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < INTERFACE: "interface" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < TRUE: "true" >
| < PRINT: "Out.print" >
| < VOID: "void" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/************************************
 * The Zilch Grammar Starts Here *
 ************************************/


void Goal() :
{}
{
    ( MethodDeclaration() )*
    MainMethodDeclaration()
    <EOF>
}

void VarDeclaration() :
{}
{
  Type() Identifier() ";"
}


void MainMethodDeclaration() :
{}
{
  "void" "main" "(" "void" ")" "{"
    ( LOOKAHEAD(2) VarDeclaration() )*
    ( Statement() )*
  "}"
}

void MethodDeclaration() :
{}
{
  Type() Identifier()
  "(" ( FormalParameterList() )? ")"
  "{" ( LOOKAHEAD(2) VarDeclaration() )*
      ( Statement() )* "return" Expression() ";" "}"
}

void FormalParameterList() :
{}
{
  FormalParameter() FormalParameterTail()
}

void FormalParameter() :
{}
{
  Type() Identifier()
}

void FormalParameterTail() :
{}
{
  ( FormalParameterTerm() )*
}

void FormalParameterTerm() :
{}
{
  "," FormalParameter()
}

void Type() :
{}
{
  LOOKAHEAD(3)
  ArrayType()
|
  BooleanType()
|
  IntegerType()
|
  Identifier()
}

void ArrayType() :
{}
{
  "int" "[" "]"
}

void BooleanType() :
{}
{
  "boolean"
}

void IntegerType() :
{}
{
  "int"
}

void Statement() :
{}
{
  Block()
|
  LOOKAHEAD(2)
  AssignmentStatement()
|
  LOOKAHEAD(2)
  ArrayAssignmentStatement()
// |
//   LOOKAHEAD( Identifier() "++" ";" )
//   PlusPlusExpression()
// |
//   LOOKAHEAD( Identifier() "--" ";" )
//   MinusMinusExpression()
|
  IfStatement()
|
  WhileStatement()
|
  PrintStatement()
|
  ReadPrimaryTape()
|
  ReadPrivateTape()
|
  SeekPrimaryTape()
|
  SeekPrivateTape()
|
  AnswerStatement()
}

void Block() :
{}
{
  "{" ( Statement() )* "}"
}

void AssignmentStatement() :
{}
{
  Identifier() "=" Expression() ";"
}

void ArrayAssignmentStatement() :
{}
{
  Identifier() "[" Expression() "]" "=" Expression() ";"
}

void IfStatement() :
{}
{
  "if" "(" Expression() ")" Statement() "else" Statement()
}

void WhileStatement() :
{}
{
  "while" "(" Expression() ")" Statement()
}

void PrintStatement() :
{}
{
  "Out.print" "(" Expression() ")" ";"
}

void ReadPrimaryTape() :
{}
{
  "PrimaryTape.read" "(" Expression() ")" ";"
}

void ReadPrivateTape() :
{}
{
  "PrivateTape.read" "(" Expression() ")" ";"
}

void SeekPrimaryTape() :
{}
{
  "PrimaryTape.seek" "(" Expression() "," Expression() ")" ";"
}

void SeekPrivateTape() :
{}
{
  "PrivateTape.seek" "(" Expression() "," Expression() ")" ";"
}

void AnswerStatement() :
{}
{
  "Prover.answer" "(" Expression() ")" ";"
}


void Expression() :
{}
{
  LOOKAHEAD( Clause() "&&" )
  AndExpression()
|
  LOOKAHEAD( Clause() "||" )
  OrExpression()
|
  LOOKAHEAD( PrimaryExpression() "==" )
  EqExpression()
|
  LOOKAHEAD( PrimaryExpression() "<" )
  LessThanExpression()
|
  LOOKAHEAD( PrimaryExpression() ">" )
  GreaterThanExpression()
|
  LOOKAHEAD( PrimaryExpression() "<=" )
  LessEqualThanExpression()
|
  LOOKAHEAD( PrimaryExpression() ">=" )
  GreaterEqualThanExpression()
|
  LOOKAHEAD( PrimaryExpression() "+" )
  PlusExpression()
|
  LOOKAHEAD( PrimaryExpression() "-" )
  MinusExpression()
|
  LOOKAHEAD( PrimaryExpression() "*" )
  TimesExpression()
|
  LOOKAHEAD( PrimaryExpression() "[" )
  ArrayLookup()
|
  LOOKAHEAD( PrimaryExpression() "." Identifier() "(" )
  MessageSend()
|
  LOOKAHEAD( Identifier() "(" )
  MethodCall()
|
  Clause()
}

void AndExpression() :
{}
{
  Clause() "&&" Clause()
}

void OrExpression() :
{}
{
  Clause() "||" Clause()
}


void EqExpression() :
{}
{
  PrimaryExpression() "==" PrimaryExpression()
}

void LessThanExpression() :
{}
{
  PrimaryExpression() "<" PrimaryExpression()
}

void GreaterThanExpression() :
{}
{
  PrimaryExpression() ">" PrimaryExpression()
}

void LessEqualThanExpression() :
{}
{
  PrimaryExpression() "<=" PrimaryExpression()
}

void GreaterEqualThanExpression() :
{}
{
  PrimaryExpression() ">=" PrimaryExpression()
}

void PlusExpression() :
{}
{
  PrimaryExpression() "+" PrimaryExpression()
}

// void PlusPlusExpression() :
// {}
// {
//   Identifier() "++" ";"
// }

void MinusExpression() :
{}
{
  PrimaryExpression() "-" PrimaryExpression()
}

// void MinusMinusExpression() :
// {}
// {
//   Identifier() "--" ";"
// }

void TimesExpression() :
{}
{
  PrimaryExpression() "*" PrimaryExpression()
}

void ArrayLookup() :
{}
{
  PrimaryExpression() "[" PrimaryExpression() "]"
}

void MessageSend() :
{}
{
  PrimaryExpression() "." Identifier()
  "(" ( ExpressionList() )? ")"
}

void MethodCall() :
{}
{
  Identifier() "(" ( ExpressionList() )? ")"
}

void ExpressionList() :
{}
{
  Expression() ExpressionTail()
}

void ExpressionTail() :
{}
{
  ( ExpressionTerm() )*
}

void ExpressionTerm() :
{}
{
  "," Expression()
}

void Clause() :
{}
{
    NotExpression()
  | PrimaryExpression()
}

void PrimaryExpression() :
{}
{
  IntegerLiteral()
|
  TrueLiteral()
|
  FalseLiteral()
|
  Identifier()
|
  LOOKAHEAD(3)
  ArrayAllocationExpression()
|
  BracketExpression()
}

void IntegerLiteral() :
{}
{
  <INTEGER_LITERAL>
}


void TrueLiteral() :
{}
{
  "true"
}

void FalseLiteral() :
{}
{
  "false"
}

void Identifier() :
{}
{
  <IDENTIFIER>
}

void ArrayAllocationExpression() :
{}
{
  "new" "int" "[" Expression() "]"
}

void NotExpression() :
{}
{
  "!" Clause()
}

void BracketExpression() :
{}
{
  "(" Expression() ")"
}
